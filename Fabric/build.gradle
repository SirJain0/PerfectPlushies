plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "0.3.3"
}


dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.20.1:${geckolib_version}"
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${project.forge_config_api_port_version}"
    include "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${project.forge_config_api_port_version}"
    modImplementation include("com.nyfaria.perfectplushieapi:perfectplushieapi-Fabric-1.20.1:${plushie_api_version}"){
        transitive = false
    }
}

loom {
    accessWidenerPath = file("src/main/resources/META-INF/perfectplushies.accesswidener")
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
}
reg.configureJarTask(remapJar)
publishMods {
    if(publish_fabric.asBoolean()) {
        file = remapJar.archiveFile
        changelog = project(":Common").file("changelog.md").text
        type = STABLE
        modLoaders.add("fabric")
        displayName = "${mod_name} ${project.property('version')} [Fabric]"
        dryRun = dry_run.toBoolean()
        if (curseforge_publishing_enabled.toBoolean()) {
            curseforge {
                projectId = curseforge_project_id
                accessToken = curseforge_token
                minecraftVersions.add(minecraft_version)
                requires {
                    slug = "geckolib"
                }
            }
        }
        if (modrinth_publishing_enabled.toBoolean()) {
            modrinth {
                accessToken = modrinth_token
                projectId = modrinth_project_id
                minecraftVersions.add(minecraft_version)
                requires {
                    slug = "geckolib"
                }
            }
        }
    }
}
