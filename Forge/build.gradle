plugins {
    id 'maven-publish'
    id 'com.matyrobbrt.mc.registrationutils'
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id "me.modmuss50.mod-publish-plugin" version "0.3.3"
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"
    if (project.hasProperty('forge_ats_enabled') && project.findProperty('forge_ats_enabled').toBoolean()) {
        // This location is hardcoded in Forge and can not be changed.
        // https://github.com/MinecraftForge/MinecraftForge/blob/be1698bb1554f9c8fa2f58e32b9ab70bc4385e60/fmlloader/src/main/java/net/minecraftforge/fml/loading/moddiscovery/ModFile.java#L123
        accessTransformers = [project(":Common").file('src/main/resources/META-INF/accesstransformer.cfg')]
        project.logger.debug('Forge Access Transformers are enabled for this project.')
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
    runs {
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }

    }
}

sourceSets.main.resources.srcDir '../Common/src/generated/resources'
mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
}
dependencies {
    compileOnly project(":Common")
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    modImplementation "software.bernie.geckolib:geckolib-forge-1.20.1:${geckolib_version}"
//    compileOnly("com.nyfaria.perfectplushieapi:perfectplushieapi-Common-1.20.1:${plushie_api_version}")
    jarJar(modImplementation("com.nyfaria.perfectplushieapi:perfectplushieapi-Forge-1.20.1:${plushie_api_version}"))
}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json"
    ])
}

//reg.configureJarTask(tasks.jar)
publishMods {
    if(publish_forge) {
        file = tasks.jar.archiveFile
        changelog = project(":Common").file("changelog.md").text
        type = STABLE
        modLoaders.add("forge")
        displayName = "${mod_name} ${project.property('version')} [Forge]"
        dryRun = dry_run.toBoolean()
        if (curseforge_publishing_enabled.toBoolean()) {
            curseforge {
                projectId = curseforge_project_id
                accessToken = curseforge_token
                minecraftVersions.add(minecraft_version)
                requires {
                    slug = "geckolib"
                }
            }
        }
        if (modrinth_publishing_enabled.toBoolean()) {
            modrinth {
                accessToken = modrinth_token
                projectId = modrinth_project_id
                minecraftVersions.add(minecraft_version)
                requires {
                    slug = "geckolib"
                }
            }
        }
    }
}